grammar br.edu.ufrn.myhtml.HtmlModel with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate htmlModel "http://www.edu.br/ufrn/myhtml/HtmlModel"


MyHtmlModel:
	article = Article;

Article:
	{Article} 'Article' '{'
	(sections+=Section)*
	references=References?
	'}';
	
Section:
    'Section' '(' 'title' '=' title=STRING (',' 'size' '=' size=HINT )? ')' '{'
	(paragraphs+=Paragraph)*
	(tables+=Table)*
	'}';

Table:
	'Table' '{'
		header = Header
		(rows+=Row)+
	'}';

Header:
	{Header} 'Header' '{'
		(tableHeaderData+=STRING)*
	'}';

Row:
	{Row} 'Row' '{'
		(tableRowData+=STRING)*
	'}';

Paragraph:
    'Paragraph' ('(' 'color' '=' color=BasicColors ')')? '{'
    content=STRING
    '}';
        
References:
    {References} 'References' '{'
        (links+=Link)*
    '}';

Link:
    ID('.' ID)*;

terminal HINT returns ecore::EInt: ('1'..'6');

enum BasicColors:
    black |
    red |
    blue |
    green;